import pandas as pd
from pathlib import Path
from functools import lru_cache
import shlex
from paulssonlab.shenker.sequencing.common import REMOTE_HOST, REMOTE_BASE_DIR

REMOTE_DATA_DIR = f"{REMOTE_BASE_DIR}/!!Jacob Quinn Shenker/210226/20210226_0032_MN35044_afh231_b9fddc76"

@lru_cache
def get_samples():
    samples = pd.read_csv("data/samples.tsv", sep="\t", names=["sample", "references"], index_col=False)
    samples = {row.sample.replace(".fastq", ""): row.references.split(",") for row in samples.itertuples()}
    return samples

# workdir: ""

localrules: all, download_samples, download_reads, have_samples, any2fasta, merge_fasta

rule all:
    input:
        "output/.done"

rule download_samples:
    output:
        "data/samples.tsv",
    shell:
        f"scp \"{REMOTE_HOST}:{shlex.quote(REMOTE_DATA_DIR)}/samples.tsv\" {{output}}"

rule download_reads:
    output:
        directory("data/fastq_pass")
    shell:
        f"scp -r \"{REMOTE_HOST}:{shlex.quote(REMOTE_DATA_DIR)}/fastq_pass\" {{output}}"

checkpoint have_samples:
    input:
        "data/samples.tsv", "data/fastq_pass"
    output:
        touch("output/.readytorun")

rule any2fasta:
    input:
        "references/{reference}.gb"
    output:
        temp("references/{reference}.fasta")
    conda:
        "envs/any2fasta.yml"
    shell:
        "any2fasta -q {input} | seqkit replace -p '(.*)' -r '{wildcards.reference}' > {output}"

def wait_merge_fasta(wildcards):
    checkpoints.have_samples.get(**wildcards)
    samples = get_samples()
    files = expand("references/{reference}.fasta", reference=samples[wildcards.sample])
    return files

rule merge_fasta:
    input:
        wait_merge_fasta
    output:
        "output/{sample}/reference.fasta"
    wildcard_constraints:
        input=".+"
    shell:
        "cat {input} > {output}"

rule merge_reads:
    input:
        lambda wildcards: Path(f"data/fastq_pass/{wildcards.sample}").glob("*.fastq")
    output:
        "output/{sample}/all_reads.fastq" # TODO: make temp?
    wildcard_constraints:
        input=".+"
    shell:
        "cat {input} > {output}"

rule minimap2:
    input:
        reads="output/{sample}/all_reads.fastq",
        target="output/{sample}/reference.fasta"
    output:
        "output/{sample}/alignments.sam"
    log:
        "logs/minimap2/{sample}.log"
    conda:
        "envs/mapping.yml"
    params:
        extra="-ax map-ont"
    threads:
        8
    shell:
        "minimap2 -t {threads} {params.extra} -o {output} {input.target} {input.reads} 2> {log}"

rule samtools_sort:
    input:
        "output/{sample}/alignments.sam"
    output:
        "output/{sample}/alignments.sorted.sam"
    conda:
        "envs/mapping.yml"
    shell:
        "samtools sort -O sam {input} -o {output}"

# rule samtools_index:
#     input:
#         "output/{sample}/alignments.sorted.sam"
#     output:
#         "output/{sample}/alignments.sorted.sam.sai"
#     conda:
#         "envs/mapping.yml"
#     shell:
#         "samtools index {input} {output}"

rule racon:
    input:
        reads="output/{sample}/all_reads.fastq",
        alignments="output/{sample}/alignments.sorted.sam",
        target="output/{sample}/reference.fasta"
    output:
        "output/{sample}/racon.fasta"
    log:
        "logs/racon/{sample}.log"
    conda:
        "envs/mapping.yml"
    params:
        # medaka's desired options plus "-u" and "--no-trimming"
        # -u: so that we aren't missing any output files
        # --no-trimming: otherwise we see deletions
        extra="-m 8 -x -6 -g -8 -w 500 -u --no-trimming"
    threads:
        8
    shell:
        "racon -t {threads} {params.extra} {input.reads} {input.alignments} {input.target} > {output} 2> {log}"

rule extract_consensus:
    input:
        "output/{sample}/racon.fasta"
    output:
        directory("output/{sample}/racon.fasta.split")
    log:
        "logs/extract_consensus/{sample}.log"
    conda:
        "envs/mapping.yml"
    params:
        outdir=lambda wildcards, input: f"{input}.split"
    shell:
        "(cat {input} | seqkit replace -p '^(\\S+).*' -r '{wildcards.sample}_$1'"
        " | seqkit split - --by-id -f -O {params.outdir}) 2> {log}"

def wait_finish(wildcards):
    checkpoints.have_samples.get(**wildcards)
    samples = get_samples()
    return expand("output/{sample}/racon.fasta.split", sample=samples.keys())

rule finish:
    input:
        wait_finish
    output:
        touch("output/.done")
